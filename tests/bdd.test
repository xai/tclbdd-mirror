# bdd.test --
#
#       Tests for Binary Decision Diagrams (BDD's)
#
# Copyright (c) 2013 by Kevin B. Kenny.

package require tcltest 2
namespace import -force ::tcltest::*
tcltest::loadTestedCommands
package require tclbdd

proc evdump {dump nvars node truth varindex} {
    if {$node < 2} {
	return $node
    }
    set v [expr {$truth & 1}]
    set truth [expr {$truth >> 1}]
    lassign [dict get $dump $node] testvar iffalse iftrue
    if {$testvar > $varindex} {
	tailcall evdump $dump $nvars $node $truth [expr {$varindex+1}]
    } elseif {$v} {
	tailcall evdump $dump $nvars $iftrue $truth [expr {$varindex+1}]
    } else {
	tailcall evdump $dump $nvars $iffalse $truth [expr {$varindex+1}]
    }
}

proc truthtable {dump nvars} {
    set root [lindex $dump 0]
    set result {}
    for {set i 0} {$i < (1<<$nvars)} {incr i} {
	append result [evdump $dump $nvars $root $i 0]
    }
    return $result
}

test bdd-1.0 {simple create/delete} {
    set s [bdd::system new]
    rename $s {}
} {}

test bdd-1.1 {create with nonstandard size} {
    rename [bdd::system new 2048] {}
} {}
test bdd-1.2 {wrong # args} {*}{
    -body {rename [bdd::system new too many] {}}
    -returnCodes error
    -match glob
    -result {wrong # args*}
}
test bdd-1.3 {size not an integer} {*}{
    -body {rename [bdd::system new garbage] {}}
    -returnCodes error
    -result {expected integer but got "garbage"}
}

test bdd-2.1 {beadindex wrong # args} {*}{
    -setup {bdd::system create sys}
    -body {sys beadindex}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-2.2 {predefined 0} {*}{
    -setup {bdd::system create sys}
    -body {sys beadindex 0}
    -cleanup {rename sys {}}
    -result 0
}

test bdd-2.3 {predefined 1} {*}{
    -setup {bdd::system create sys}
    -body {sys beadindex 1}
    -cleanup {rename sys {}}
    -result 1
}

test bdd-2.4 {expression not found} {*}{
    -setup {bdd::system create sys}
    -body {list [catch {sys beadindex garbage} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-4.1 {:= - wrong # args} {*}{
    -setup {bdd::system create sys}
    -body {sys :=}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-4.2 {:= - can't find expr} {*}{
    -setup {bdd::system create sys}
    -body {list [catch {sys := yes garbage} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-4.3 {:=} {*}{
    -setup {bdd::system create sys}
    -body {sys := yes 1; sys beadindex yes}
    -cleanup {rename sys {}}
    -result 1
}

test bdd-5.1 {dump - wrong # args} {*}{
    -setup {bdd::system create sys}
    -body {sys dump}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-5.2 {dump - leaf 0} {*}{
    -setup {bdd::system create sys}
    -body {sys dump 0}
    -cleanup {rename sys {}}
    -result {0 {0 0 0}}
}

test bdd-5.3 {dump - leaf 1} {*}{
    -setup {bdd::system create sys}
    -body {sys dump 1}
    -cleanup {rename sys {}}
    -result {1 {0 1 1}}
}

test bdd-6.1 {nthvar - wrong # args} {
    -setup {bdd::system create sys}
    -body {sys nthvar}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-6.2 {nthvar - bad var index} {
    -setup {bdd::system create sys}
    -body {sys nthvar a garbage}
    -cleanup {rename sys {}}
    -returnCodes error
    -result {expected integer but got "garbage"}
}

test bdd-6.3 {nthvar - var 0} {
    -setup {bdd::system create sys}
    -body {
	sys nthvar a 0
	set result [list [sys dump a]]
	sys unset a
	lappend result [sys gc]
    }
    -cleanup {rename sys {}}
    -result {{2 {0 0 1} 0 {1 0 0} 1 {1 1 1}} 2}
}

test bdd-6.4 {nthvar - two vars} {
    -setup {bdd::system create sys}
    -body {
	sys nthvar a 0
	sys nthvar b 1
	set result [list [sys dump b] [sys dump a]]
	sys unset a
	sys unset b
	lappend result [sys gc]
    }
    -cleanup {rename sys {}}
    -result {{3 {1 0 1} 0 {2 0 0} 1 {2 1 1}} {2 {0 0 1} 0 {2 0 0} 1 {2 1 1}} 2}
}

test bdd-6.5 {nthvar - redundant var (tests duplicate BDD_MakeBead)} {
    -setup {bdd::system create sys}
    -body {
	sys nthvar a 0;
	sys nthvar b 1;
	sys nthvar c 0
	list [sys beadindex a] [sys beadindex b] [sys beadindex c] \
	    [sys unset a] [sys unset b] [sys unset c] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {2 3 2 {} {} {} 2}
}

test bdd-7.1 {notnthvar - wrong # args} {
    -setup {bdd::system create sys}
    -body {sys notnthvar}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-7.2 {notnthvar - bad var index} {
    -setup {bdd::system create sys}
    -body {sys notnthvar a garbage}
    -cleanup {rename sys {}}
    -returnCodes error
    -result {expected integer but got "garbage"}
}

test bdd-7.3 {notnthvar - var 0} {
    -setup {bdd::system create sys}
    -body {
	sys notnthvar !a 0; 
	list [sys dump !a] [sys unset !a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {{2 {0 1 0} 1 {1 1 1} 0 {1 0 0}} {} 2}
}

test bdd-7.4 {notnthvar - two vars} {
    -setup {bdd::system create sys}
    -body {
	sys notnthvar !a 0; sys notnthvar !b 1
	list [sys dump !b] [sys dump !a] \
	    [sys unset !b] [sys unset !a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {{3 {1 1 0} 1 {2 1 1} 0 {2 0 0}} {2 {0 1 0} 1 {2 1 1} 0 {2 0 0}} {} {} 2}
}

test bdd-7.5 {notnthvar - redundant var (tests duplicate BDD_MakeBead)} {
    -setup {bdd::system create sys}
    -body {
	sys notnthvar !a 0; sys notnthvar !b 1; sys notnthvar !c 0
	list [sys beadindex !a] [sys beadindex !b] [sys beadindex !c] \
	    [sys unset !a] [sys unset !b] [sys unset !c] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {2 3 2 {} {} {} 2}
}

test bdd-7.6 {unset} {*}{
    -setup {bdd::system create sys}
    -body {
	sys nthvar a 0
	sys unset a
	list [catch {sys := b a} result] $result $::errorCode [sys gc]
    }
    -cleanup {sys destroy}
    -result {1 {expression named "a" not found} {BDD ExprNotFound a} 2} 
}

test bdd-8.1 {negate - wrong # args} {
    -setup {bdd::system create sys}
    -body {sys ~}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-8.2 {negate - bad expr} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
    }
    -body {list [catch {sys ~ b garbage} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-8.3 {negate 0} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
    }
    -body {
	sys ~ b 0; 
	list [sys beadindex b] \
	    [sys unset b] [sys unset !a] [sys unset a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} {} {} 2}
}

test bdd-8.4 {negate 1} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
    }
    -body {sys ~ b 1; sys beadindex b}
    -cleanup {rename sys {}}
    -result 0
}

test bdd-8.5 {negate a} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
    }
    -body {
	sys ~ b a; 
	list [expr {[sys beadindex b] == [sys beadindex !a]}] \
	    [sys unset b] [sys unset !a] [sys unset a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} {} {} 2}
}

test bdd-8.5 {negate !a} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
    }
    -body {
	sys ~ b !a; 
	list [expr {[sys beadindex b] == [sys beadindex a]}] \
	    [sys unset b] [sys unset !a] [sys unset a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} {} {} 2}
}

test bdd-9.1 {binop - wrong # args} {
    -setup {bdd::system create sys}
    -body {sys nand}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-9.2 {binary operator - bad first expr} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 0; sys notnthvar !b 0
    }
    -body {list [catch {sys & c garbage b} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-9.3 {binary operator - bad second expr} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 0; sys notnthvar !b 0
    }
    -body {list [catch {sys & c a garbage} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-9.4 {binary nor operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys nor c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys nor c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys nor c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys nor c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {0001 0010 0100 1000 2}
}

test bdd-9.5 {binary < operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys < c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys < c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys < c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys < c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {0100 1000 0001 0010 2}
}

test bdd-9.6 {binary > operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys > c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys > c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys > c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys > c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {0010 0001 1000 0100 2}
}

test bdd-9.7 {binary ^ operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys ^ c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys ^ c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys ^ c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys ^ c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {0110 1001 1001 0110 2}
}

test bdd-9.8 {binary nand operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys nand c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys nand c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys nand c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys nand c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {0111 1011 1101 1110 2}
}
test bdd-9.9 {binary & operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys & c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys & c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys & c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys & c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {1000 0100 0010 0001 2}
}

test bdd-9.10 {binary == operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys == c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys == c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys == c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys == c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result  {1001 0110 0110 1001 2}
}

test bdd-9.11 {binary <= operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys <= c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys <= c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys <= c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys <= c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {1101 1110 0111 1011 2}
}

test bdd-9.12 {binary >= operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys >= c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys >= c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys >= c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys >= c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {1011 0111 1110 1101 2}
}

test bdd-9.13 {binary | operator} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	set tables {}
	sys | c !a !b
	lappend tables [truthtable [sys dump c] 2]
	sys | c a !b
	lappend tables [truthtable [sys dump c] 2]
	sys | c !a b
	lappend tables [truthtable [sys dump c] 2]
	sys | c a b
	lappend tables [truthtable [sys dump c] 2]
	sys unset a !a b !b c
	lappend tables [sys gc]
	set tables
    }
    -cleanup {rename sys {}}
    -result {1110 1101 1011 0111 2}
}

test bdd-10.1 {syllogism in the mode of barbara} {
    -setup {
	bdd::system create sys
	sys nthvar man(x) 0
	sys nthvar mortal(x) 1
	sys nthvar socrates(x) 2
    }
    -body {
	sys <= major man(x) mortal(x)
	sys <= minor socrates(x) man(x)
	sys & conjunct major minor
	sys <= conclusion socrates(x) mortal(x)
	sys <= test0 major conclusion;    # does the major premise imply 
	;			          # the conclusion?
	sys <= test1 minor conclusion;    # does the minor premise imply
	;			          # the conclusion?
	sys <= test2 conjunct conclusion; # is the syllogism valid?
	list \
	    [expr {[sys beadindex test0] == 1}] \
	    [expr {[sys beadindex test1] == 1}] \
	    [expr {[sys beadindex test2] == 1}] \
	    [truthtable [sys dump conjunct] 3] \
	    [sys unset test2 test1 test0 conclusion conjunct minor major \
		 socrates(x) mortal(x) man(x)] \
	    [sys gc]
    } 
    -cleanup {rename sys {}}
    -result {0 0 1 10110001 {} 2}
}

test bdd-11.1 {demorgan} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	sys & c a b
	sys nor d !a !b
	list [expr {[sys beadindex c] == [sys beadindex d]}] \
	    [sys unset d c !b b !a a] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {
	rename sys {}
   }
}

test bdd-11.2 {demorgan} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	sys | c a b
	sys nand d !a !b
	list [expr {[sys beadindex c] == [sys beadindex d]}] \
	    [sys unset d c !b b !a a] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {
	rename sys {}
   }
}

test bdd-11.3 {demorgan} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	sys ^ c a b
	sys ^ d !a !b
	list [expr {[sys beadindex c] == [sys beadindex d]}] \
	    [sys unset d c !b b !a a] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {
	rename sys {}
   }
}

test bdd-11.4 {demorgan} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	sys & c a b
	sys | d !a !b
	sys ~ d d
	list [expr {[sys beadindex c] == [sys beadindex d]}] \
	    [sys unset d c !b b !a a] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {
	rename sys {}
   }
}

test bdd-11.5 {demorgan} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	sys | c a b
	sys & d !a !b
	sys ~ d d
	list [expr {[sys beadindex c] == [sys beadindex d]}] \
	    [sys unset d c !b b !a a] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {
	rename sys {}
   }
}

test bdd-11.6 {demorgan} {
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	sys ^ c a b
	sys == d !a !b
	sys ~ d d
	list [expr {[sys beadindex c] == [sys beadindex d]}] \
	    [sys unset d c !b b !a a] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {
	rename sys {}
   }
}

test bdd-12.1 {satcount - wrong # args} {
    -setup {bdd::system create sys}
    -body {sys satcount}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-12.2 {satcount - no such expr} {
    -setup {bdd::system create sys}
    -body {list [catch {sys satcount garbage} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-12.3 {satcount - zero} {
    -setup {bdd::system create sys}
    -body {
	list [sys satcount 0] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {0 2}
}
    
test bdd-12.4 {satcount - no vars} {
    -setup {bdd::system create sys}
    -body {
	list [sys satcount 1] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 2}
}

test bdd-12.5 {satcount - one var} {
    -setup {bdd::system create sys; sys nthvar a 0}
    -body {
	list [sys satcount 1] [sys unset a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {2 {} 2}
}

test bdd-12.6 {satcount - two vars} {
    -setup {bdd::system create sys; sys nthvar a 1}
    -body {
	list [sys satcount 1] [sys unset a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {4 {} 2}
}

test bdd-12.7 {satcount - three vars} {
    -setup {bdd::system create sys; sys nthvar a 2}
    -body {
	list [sys satcount 1] [sys unset a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result {8 {} 2}
}

test bdd-12.8 {satcount - one var of three} {
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
    }
    -body {
	list [sys satcount a] [sys satcount b] [sys satcount c] \
	    [sys unset c b a] [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {4 4 4 {} 2}
}

test bdd-12.9 {satcount - two vars of three} {
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
    }
    -body {
	sys | r1 a b
	sys | r2 a c
	sys | r3 b c
	list [sys satcount r1] [sys satcount r2] [sys satcount r3] \
	    [sys unset r3 r2 r1 c b a] [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {6 6 6 {} 2}
}

test bdd-12.10 {satcount - complex expr} {
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
	sys nthvar d 3
	sys nthvar e 4
	sys nthvar f 5
	sys nthvar g 6
	sys nthvar h 7
	sys nthvar j 8
    }
    -body {
	sys | r1 a b
	sys & r2 c d
	sys | r2 r2 e
	sys & r1 r1 r2
	sys | r2 f g
	sys | r2 r2 h
	sys & r1 r1 r2
	sys ~ r2 r1
	list [sys satcount r1] [sys satcount r2] \
	    [sys unset r2 r1 j h g f e d c b a] \
	    [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {210 302 {} 2}
}

test bdd-12.11 {satcount - parity tree} {
    -setup {
	bdd::system create sys
	for {set i 0} {$i < 128} {incr i} {
	    sys nthvar v$i $i
	}
    }
    -body {
	sys := r0 0
	for {set i 0} {$i < 128} {incr i 2} {
	    sys ^ r0 r0 v$i
	}
	for {set i 1} {$i < 128} {incr i 2} {
	    sys ^ r0 r0 v$i
	}
	set result [list [dict size [sys dump r0]] \
			[format %llx [sys satcount r0]]]
	sys unset r0
	for {set i 0} {$i < 128} {incr i} {
	    sys unset v$i
	}
	lappend result [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {257 80000000000000000000000000000000 2}
}

test bdd-13.1 {restrict - wrong # args} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
    }
    -body {
	sys restrict r1
    }
    -cleanup {
	rename sys {}
    }
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-13.2 {restrict - can't find expr to restrict} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
    }
    -body {list [catch {sys restrict r1 garbage} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-13.3 {restrict - can't find second expression} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
	sys ^ r2 c b
	sys ^ r2  a r2
    }
    -body {list [catch {sys restrict r1 r2 garbage} result] \
	       $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-13.4 {restrict - not a literal} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
	sys nthvar c 2; sys notnthvar !c 2
	sys ^ r2 c b
	sys ^ r2  a r2
    }
    -body {list [catch {sys restrict r1 c r2} result] \
	       $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {r2 is not a literal} {BDD NotLiteral r2}}
}

test bdd-13.5 {restrict - do nothing gracefully} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
	sys nthvar c 2; sys notnthvar !c 2
	sys ^ r2 c b
	sys ^ r2  a r2
    }
    -body {
	sys restrict r1 r2
	list [expr {[sys beadindex r1] == [sys beadindex r2]}] \
	    [sys unset r1 r2 c !c b !b a !a] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-13.6 {restrict by literal} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
	sys nthvar c 2; sys notnthvar !c 2
	sys ^ r2 c b
	sys ^ r2  a r2
    }
    -body {
	sys restrict r1 r2 !b
	sys ^ r3 a c 
	list [expr {[sys beadindex r1] == [sys beadindex r3]}] \
	    [sys unset r3 r2 r1 !c c !b b !a a] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-13.7 {restrict by two literals} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
	sys nthvar c 2; sys notnthvar !c 2
	sys ^ r2 c b
	sys ^ r2  a r2
    }
    -body {
	sys restrict r1 r2 a c
	list [expr {[sys beadindex r1] == [sys beadindex b]}] \
	    [sys unset r2 r1 !c c !b b !a a] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-13.8 {restrict by two disordered literals} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
	sys nthvar c 2; sys notnthvar !c 2
	sys ^ r2 c b
	sys ^ r2  a r2
    }
    -body {
	sys restrict r1 r2 c a
	list [expr {[sys beadindex r1] == [sys beadindex b]}] \
	    [sys unset r2 r1 c !c b !b a !a] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-13.9 {restrict - reduce to constant} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
    }
    -body {
	sys & r1 a b
	sys & r1 r1 c
	sys restrict r1 r1 a b
	sys restrict r2 r1 c
	list [sys beadindex r2] \
	    [sys unset r2 r1 c b a] \
	    [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {1 {} 2}
}

test bdd-13.10 {restrict zero} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
	sys nthvar c 2; sys notnthvar !c 2
	sys ^ r2 c b
	sys ^ r2  a r2
    }
    -body {
	sys restrict r1 0 a c
	list [sys beadindex r1] \
	    [sys unset r2 c !c b !b a !a] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {0 {} 2}
}

test bdd-13.11 {restrict one} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
	sys nthvar c 2; sys notnthvar !c 2
	sys ^ r2 c b
	sys ^ r2  a r2
    }
    -body {
	sys restrict r1 1 a c
	list [sys beadindex r1] \
	    [sys unset r1 r2 c b a !c !b !a] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-14.1 {foreach_sat - wrong # args} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys foreach_sat
    }
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-14.2 {foreach_sat - bad expression} {
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys foreach_sat x garbage {}} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}
    
test bdd-14.3 {foreach_sat - nothing satisfies} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys foreach_sat x 0 {error this loop should not execute}
    }
    -cleanup {
	rename sys {}
    }
    -result {}
}
    
test bdd-14.4 {foreach_sat - tautology} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	set result {}
	sys foreach_sat x 1 {
	    lappend result $x
	}
	set result
    }
    -cleanup {
	rename sys {}
    }
    -result {{}}
}

test bdd-14.5 {foreach_sat - failure to write loop variable} {
    -setup {
	set badx(0) 1
	bdd::system create sys
    }
    -body {
	set result {}
	sys foreach_sat badx 1 {
	}
    }
    -cleanup {
	rename sys {}
	unset badx
    }
    -returnCodes error
    -match glob
    -result {*variable is array*}
}

test bdd-14.6 {foreach_sat - usual case} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2; sys nthvar d 3
    }
    -body {
	set result {}
	sys := z a
	sys & y b c
	sys | z z y
	sys | z z d
	sys foreach_sat x z {
	    lappend result $x
	}
	sys unset z y a b c d
	lappend result [sys gc]
	set result
    }
    -cleanup {
	rename sys {}
    }
    -result {{0 0 1 0 3 1} {0 0 1 1 2 0 3 1} {0 0 1 1 2 1} {0 1} 2}
}

test bdd-14.7 {foreach_sat - continue} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2; sys nthvar d 3
    }
    -body {
	set result {}
	sys := z a
	sys & y b c
	sys | z z y
	sys | z z d
	sys foreach_sat x z {
	    if {[dict exists $x 3]} continue
	    lappend result $x
	}
	sys unset z y a b c d
	lappend result [sys gc]
	set result
    }
    -cleanup {
	rename sys {}
    }
    -result {{0 0 1 1 2 1} {0 1} 2}
}

test bdd-14.8 {foreach_sat - break} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2; sys nthvar d 3
    }
    -body {
	set result {}
	sys := z a
	sys & y b c
	sys | z z y
	sys | z z d
	sys foreach_sat x z {
	    if {![dict exists $x 3]} break
	    lappend result $x
	}
	sys unset z y a b c d
	lappend result [sys gc]
	set result
    }
    -cleanup {
	rename sys {}
    }
    -result {{0 0 1 0 3 1} {0 0 1 1 2 0 3 1} 2}
}

test bdd-14.9 {foreach_sat - return} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2; sys nthvar d 3
	proc bdd-14.9 {} {
	    set result {}
	    sys foreach_sat x z {
		if {![dict exists $x 3]} {
		    return $result
		}
		lappend result $x
	    }
	    error this line should not be reached
	}
    }
    -body {
	sys := z a
	sys & y b c
	sys | z z y
	sys | z z d
	set result [bdd-14.9]
	sys unset z y a b c d
	lappend result [sys gc]
	set result
    }
    -cleanup {
	rename sys {}
	rename bdd-14.9 {}
    }
    -result {{0 0 1 0 3 1} {0 0 1 1 2 0 3 1} 2}
}

test bdd-14.10 {foreach_sat - error} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2; sys nthvar d 3
    }
    -body {
	set result {}
	sys := z a
	sys & y b c
	sys | z z y
	sys | z z d
	set result [list \
			[catch {
			    sys foreach_sat x z {
				if {![dict exists $x 3]} {
				    error testing
				}
				lappend result $x
			    }
			} result] \
			$result \
			$::errorInfo]
	sys unset z y a b c d
	lappend result [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -match glob
    -result {1 testing {testing
    while executing
"error testing"
   ('foreach_sat' body, line 3)
    invoked from within
*} 2}
}

test bdd-14.11 {foreach_sat - unusual return} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2; sys nthvar d 3
    }
    -body {
	set result {}
	sys := z a
	sys & y b c
	sys | z z y
	sys | z z d
	set result \
	    [list \
		 [catch {
		     sys foreach_sat x z {
			 if {![dict exists $x 3]} {
			     return -code 6 -level 0 testing
			 }
			 lappend result $x
		     }
		 } result] \
		 $result]
	sys unset y z a b c d
	lappend result [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {6 testing 2}
}

test bdd-15.1.1 {quantifiers - wrong # args} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys exists
    }
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-15.1.2 {quantifiers - wrong # args} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys forall
    }
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-15.2.1 {quantifiers - bad expression} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys exists x {} garbage} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-15.2.2 {quantifiers - bad expression} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys forall x {} garbage} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-15.3.1 {quantifiers - ill-formed list} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys exists x \{ 1
    }
    -cleanup {
	rename sys {}
    }
    -returnCodes error
    -result {unmatched open brace in list}
}

test bdd-15.3.2 {quantifiers - ill-formed list} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys forall x \{ 1
    }
    -cleanup {
	rename sys {}
    }
    -returnCodes error
    -result {unmatched open brace in list}
}

test bdd-15.4.1 {quantifiers - bad variable} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys exists x {garbage} 1} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-15.4.2 {quantifiers - bad variable} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys forall x {garbage} 1} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-15.5.1 {quantifiers - bad variable} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys exists x {1} 1} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {1 is not a variable} {BDD NotVariable 1}}
}

test bdd-15.5.2 {quantifiers - bad variable} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys forall x {1} 1} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {1 is not a variable} {BDD NotVariable 1}}
}

test bdd-15.6.1 {quantifiers - bad variable} {*}{
    -setup {
	bdd::system create sys
	sys notnthvar !a 0
    }
    -body {
	list [catch {sys exists x !a 1} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {!a is not a variable} {BDD NotVariable !a}}
}

test bdd-15.6.2 {quantifiers - bad variable} {*}{
    -setup {
	bdd::system create sys
	sys notnthvar !a 0
    }
    -body {
	list [catch {sys forall x !a 1} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {!a is not a variable} {BDD NotVariable !a}}
}

test bdd-15.7.1 {quantifiers - bad variable} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys ^ c a b
    }
    -body {
	list [catch {sys exists x c 1} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result {1 {c is not a variable} {BDD NotVariable c}}
}

test bdd-15.7.2 {quantifiers - bad variable} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys ^ c a b
    }
    -body {
	list [catch {sys forall x c 1} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result {1 {c is not a variable} {BDD NotVariable c}}
}

test bdd-15.8.1 {quantifiers - do nothing gracefully} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys ^ c a b
    }
    -body {
	set result {}
	foreach ex {0 1 a b c} {
	    sys exists z {} $ex
	    lappend result [expr {[sys beadindex z] == [sys beadindex $ex]}]
	}
	sys unset z c b a
	lappend result [sys gc]
	set result
    }
    -cleanup {rename sys {}}
    -result {1 1 1 1 1 2}
}

test bdd-15.8.2 {quantifiers - do nothing gracefully} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys ^ c a b
    }
    -body {
	set result {}
	foreach ex {0 1 a b c} {
	    sys forall z {} $ex
	    lappend result [expr {[sys beadindex z] == [sys beadindex $ex]}]
	}
	sys unset z c a b
	lappend result [sys gc]
	set result
    }
    -cleanup {rename sys {}}
    -result {1 1 1 1 1 2}
}

test bdd-15.9.1 {quantifiers - irrelevant variable} {*}{
    -setup {
	bdd::system create sys
	sys nthvar p 0
	sys nthvar a 1
	sys nthvar q 2
	sys nthvar b 3
	sys nthvar r 4
	sys ^ c a b
    }
    -body {
	set result {}
	foreach v {p q r} {
	    foreach ex {0 1 a b c} {
		sys exists z $v $ex
		lappend result [expr {[sys beadindex z] == [sys beadindex $ex]}]
	    }
	}
	sys unset z p q r a b c
	lappend result [sys gc]
	set result
    }
    -cleanup {rename sys {}}
    -result {1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 2}
}

test bdd-15.9.2 {quantifiers - irrelevant variable} {*}{
    -setup {
	bdd::system create sys
	sys nthvar p 0
	sys nthvar a 1
	sys nthvar q 2
	sys nthvar b 3
	sys nthvar r 4
	sys ^ c a b
    }
    -body {
	set result {}
	foreach v {p q r} {
	    foreach ex {0 1 a b c} {
		sys forall z $v $ex
		lappend result [expr {[sys beadindex z] == [sys beadindex $ex]}]
	    }
	}
	sys unset z p q r a b c
	lappend result [sys gc]
	set result
    }
    -cleanup {rename sys {}}
    -result {1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 2}
}

test bdd-15.10 {existential quantifier} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2; sys nthvar d 3
	sys nthvar e 4; sys notnthvar !e 4
	sys nthvar f 5; sys notnthvar !f 5
	sys & expr !e !f; sys & expr expr a
	sys & term !e  f; sys & term term b; sys | expr expr term
	sys & term  e !f; sys & term term c; sys | expr expr term
	sys & term  e  f; sys & term term d; sys | expr expr term
    }
    -body {
	sys | ye a b
	sys | ye ye c
	sys | ye ye d
	sys exists ze {e f} expr
	list [expr {[sys beadindex ze] == [sys beadindex ye]}] \
	    [sys unset ye ze term expr a b c d e f !e !f] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-15.11 {universal quantifier} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2; sys nthvar d 3
	sys nthvar e 4; sys notnthvar !e 4
	sys nthvar f 5; sys notnthvar !f 5
	sys & expr !e !f; sys & expr expr a
	sys & term !e  f; sys & term term b; sys | expr expr term
	sys & term  e !f; sys & term term c; sys | expr expr term
	sys & term  e  f; sys & term term d; sys | expr expr term
    }
    -body {
	sys & yu a b
	sys & yu yu c
	sys & yu yu d
	sys forall zu {e f} expr
	list [expr {[sys beadindex zu] == [sys beadindex yu]}] \
	    [sys unset zu yu expr term e !e f !f a b c d] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

foreach operation {
    &3 ^3 ?: borrow concur3 differ3 even3 median nand3 nor3 oneof3 twoof3 |3
} {
    test bdd-16.1-$operation "$operation - wrong # args" {*}{
	-setup {
	    bdd::system create sys
	}
	-body {
	    sys $operation
	}
	-cleanup {rename sys {}}
	-returnCodes error
	-match glob
	-result {wrong # args: *}
    }	
}

foreach operation {
    &3 ^3 ?: borrow concur3 differ3 even3 median nand3 nor3 oneof3 twoof3 |3
} {
    test bdd-16.2-$operation {$operation - bad first expr} {*}{
	-setup {
	    bdd::system create sys
	    sys nthvar a 0; sys notnthvar !a 0
	    sys nthvar b 0; sys notnthvar !b 0
	    sys nthvar c 0; sys notnthvar !c 0
	}
	-body {list [catch {sys $operation z garbage b c} result] \
		   $result $::errorCode}
	-cleanup {rename sys {}}
	-result \
	    {1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
    }
}

foreach operation {
    &3 ^3 ?: borrow concur3 differ3 even3 median nand3 nor3 oneof3 twoof3 |3
} {
    test bdd-16.3-$operation {$operation - bad second expr} {*}{
	-setup {
	    bdd::system create sys
	    sys nthvar a 0; sys notnthvar !a 0
	    sys nthvar b 0; sys notnthvar !b 0
	    sys nthvar c 0; sys notnthvar !c 0
	}
	-body {
	    list [catch {sys $operation z a garbage c} result] \
		$result $::errorCode \
		[sys unset a !a b !b c !c] \
		[sys gc]
	     }
	-cleanup {rename sys {}}
	-result \
	    {1 {expression named "garbage" not found} {BDD ExprNotFound garbage} {} 2}
    }
}

foreach operation {
    &3 ^3 ?: borrow concur3 differ3 even3 median nand3 nor3 oneof3 twoof3 |3
} {
    test bdd-16.4-$operation {$operation - bad third expr} {*}{
	-setup {
	    bdd::system create sys
	    sys nthvar a 0; sys notnthvar !a 0
	    sys nthvar b 0; sys notnthvar !b 0
	    sys nthvar c 0; sys notnthvar !c 0
	}
	-body {
	    list [catch {sys $operation z a b garbage} result] \
		$result $::errorCode \
		[sys unset a b c !a !b !c] \
		[sys gc]
	}
	-cleanup {rename sys {}}
	-result \
	    {1 {expression named "garbage" not found} {BDD ExprNotFound garbage} {} 2}
    }
}

test bdd-16.5 {ternary nor} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
    }
    -body {
	sys nor3 y p q r
	sys | t p q
	sys | t t r
	sys ~ z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z y t p q r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.6 {ternary oneof} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys oneof3 y p q r
	sys & z p !q
	sys & z z !r
	sys & t !p q
	sys & t t !r
	sys | z z t
	sys & t !p !q
	sys & t t r
	sys | z z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z t y p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.7 {ternary twoof} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys twoof3 y p q r
	sys & z p q
	sys & z z !r
	sys & t p !q
	sys & t t r
	sys | z z t
	sys & t !p q
	sys & t t r
	sys | z z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z y t p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-16.7 {ternary even} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys even3 y p q r
	sys ^ z p q
	sys ^ z z !r
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z y p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.8 {ternary differ} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys differ3 y p q r
	sys &3 z p q r
	sys &3 t !p !q !r
	sys nor z z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset y z t p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.9 {ternary nand} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys nand3 y p q r
	sys &3 z p q r
	sys ~ z z
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z y p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-16.10 {ternary concurrence} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys concur3 y p q r
	sys &3 z p q r
	sys &3 t !p !q !r
	sys | z z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z t y !p !q !r p q r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.11 {ternary - borrow bit of subtraction} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys borrow y p q r
	sys &3 z !p !q  r
	sys &3 t !p  q !r
	sys | z z t
	sys &3 t !p  q  r
	sys | z z t
	sys &3 t  p  q  r
	sys | z z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z t y p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.12 {ternary - borrow bit of subtraction} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys borrow y p q r
	sys &3 z !p !q  r
	sys &3 t !p  q !r
	sys | z z t
	sys &3 t !p  q  r
	sys | z z t
	sys &3 t  p  q  r
	sys | z z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z t y p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.13 {ternary - xor} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys ^3 y p q r
	sys ^ z p q
	sys ^ z z r
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z t y p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.14 {ternary - ?:} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys ?: y p q r
	sys & z p q
	sys & t !p r
	sys | z z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z t y !p !q !r p q r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.15 {ternary - median} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys median y p q r
	sys &3 z p q !r
	sys &3 t p !q r
	sys | z z t
	sys &3 t !p q r
	sys | z z t
	sys &3 t p q r
	sys | z z t
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z t y p q r !p !q !r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-16.16 {ternary - or} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys nthvar b 1; sys nthvar c 2;
	sys nthvar d 3; sys nthvar e 4; sys nthvar f 5;
	sys & p a d
	sys & q b e
	sys & r c f
	sys ~ !p p
	sys ~ !q q
	sys ~ !r r
    }
    -body {
	sys |3 y p q r
	sys | z p q
	sys | z z r
	list [expr {[sys beadindex y] == [sys beadindex z]}] \
	    [sys unset z y !p !q !r p q r a b c d e f] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-17.1 {compose - wrong # args} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys compose
    }
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-17.2 {compose - wrong # args} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys compose a b c
    }
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-17.3 {compose - bad expr} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys compose a garbage} result] $result $::errorCode
    }
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-17.4 {compose - bad var} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
    }
    -body {
	list [catch {sys compose x a garbage 0} result] $result $::errorCode \
	    [sys unset a] [sys gc]
    }
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage} {} 2}
}

test bdd-17.5 {compose - bad var} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys & c a b
    }
    -body {
	list [catch {sys compose x a c 0} result] $result $::errorCode \
	    [sys unset a b c] [sys gc]
    }
    -cleanup {rename sys {}}
    -result \
	{1 {c is not a variable} {BDD NotVariable c} {} 2}
}

test bdd-17.6 {compose - bad var} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys notnthvar !b 1
    }
    -body {
	list [catch {sys compose x a !b 0} result] $result $::errorCode \
	    [sys unset a !b] [sys gc]
    }
    -cleanup {rename sys {}}
    -result \
	{1 {!b is not a variable} {BDD NotVariable !b} {} 2}
}

test bdd-17.7 {compose - bad replacement expr} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
    }
    -body {
	list [catch {sys compose x a b garbage} result] $result $::errorCode \
	    [sys unset a b] [sys gc]
    }
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage} {} 2}
}

test bdd-17.8 {compose - do nothing gracefully} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys & c a b
    }
    -body {
	sys compose d c
	list [expr {[sys beadindex d] == [sys beadindex c]}] \
	    [sys unset a b c d] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-17.9 {compose with irrelevant variable} {*}{
    -setup {
	bdd::system create sys
	sys nthvar p 0
	sys nthvar a 1
	sys nthvar q 2
	sys nthvar b 3
	sys nthvar r 4
	sys & c a b
    }
    -body {
	sys compose d c p 0
	list [expr {[sys beadindex d] == [sys beadindex c]}] \
	    [sys unset a b c d p q r] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-17.10 {compose with irrelevant variable} {*}{
    -setup {
	bdd::system create sys
	sys nthvar p 0
	sys nthvar a 1
	sys nthvar q 2
	sys nthvar b 3
	sys nthvar r 4
	sys & c a b
    }
    -body {
	sys compose d c q 0
	list [expr {[sys beadindex d] == [sys beadindex c]}] \
	    [sys unset a b c p q r d] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-17.10 {compose with irrelevant variable} {*}{
    -setup {
	bdd::system create sys
	sys nthvar p 0
	sys nthvar a 1
	sys nthvar q 2
	sys nthvar b 3
	sys nthvar r 4
	sys & c a b
    }
    -body {
	sys compose d c r 0
	list [expr {[sys beadindex d] == [sys beadindex c]}] \
	    [sys unset a b c p q r d] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 {} 2}
}

test bdd-17.11 {compose - replace with constant} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
	sys & c a b
    } -body {
	set trouble {}
	foreach s {{a 0} {a 1} {b 0} {b 1} {a 1 b 1}} \
	    y {0 b 0 a 1} \
	    {
		sys compose z c {*}$s
		if {[sys beadindex z] != [sys beadindex $y]} {
		    lappend trouble "a&b\[[lindex $s 1]/[lindex $s 0]] " \
			"should be $y"
		}
	    }
	sys unset a !a b !b c z
	lappend trouble [sys gc]
	join $trouble \n
    }
    -cleanup {rename sys {}}
    -result 2
}

test bdd-17.12 {compose - replace with variable} {*}{
    -setup {
	bdd::system create sys
	sys nthvar p 0; sys notnthvar !p 0
	sys nthvar a 1; sys notnthvar !a 1
	sys nthvar q 2; sys notnthvar !q 2
	sys nthvar b 3; sys notnthvar !b 3
	sys nthvar r 4; sys notnthvar !r 4
	sys & c a b
    }
    -body {
	set trouble {} 
	foreach x {p a q b r} {
	    sys compose d c a $x
	    sys & e $x b
	    if {[sys beadindex d] != [sys beadindex e]} {
		lappend trouble "a&$x should be the same as (a&b)\[$x/b]"
	    }
	}
	sys unset a b c p q r !a !b !c !p !q !r d e
	lappend trouble [sys gc]
	join $trouble \n
    }
    -cleanup {rename sys {}}
    -result {2}
}

test bdd-17.13 {compose - interchange variables} {
    -setup {
	bdd::system create sys
	sys nthvar a 1; sys notnthvar !a 1
	sys nthvar b 2; sys notnthvar !b 2
	sys < c a b
	sys < y b a
    } 
    -body {
	sys compose z c a b b a
	list [expr {[sys beadindex z] == [sys beadindex y]}] \
	    [sys unset z y c a b !a !b] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-17.14 {compose - interchange variables} {
    -setup {
	bdd::system create sys
	sys nthvar a 1; sys notnthvar !a 1
	sys nthvar b 2; sys notnthvar !b 2
	sys nthvar c 3; sys notnthvar !c 3
	sys <= x a b
	sys <= t b c
	sys & x x t
	sys <= y b c
	sys <= t c a
	sys & y y t
    } 
    -body {
	sys compose z x a b b c c a
	list [expr {[sys beadindex z] == [sys beadindex y]}] \
	    [sys unset z y t x a b c !a !b !c] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-17.15 {compose - interchange variables} {
    -setup {
	bdd::system create sys
	sys nthvar a 1; sys notnthvar !a 1
	sys nthvar b 2; sys notnthvar !b 2
	sys nthvar c 3; sys notnthvar !c 3
	sys <= x a b
	sys <= t b c
	sys & x x t
	sys <= y c a
	sys <= t a b
	sys & y y t
    } 
    -body {
	sys compose z x b a c b a c
	list [expr {[sys beadindex z] == [sys beadindex y]}] \
	    [sys unset z t y x a b c !a !b !c] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-17.16 {compose - more complex substitution} {
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
	sys nthvar d 3
	sys | x a b
	sys & t a b
	sys & u c d
	sys | y t u
    } 
    -body {
	sys compose z x a t b u
	list [expr {[sys beadindex z] == [sys beadindex y]}] \
	    [sys unset x y z t u a b c d] \
	    [sys gc]
    }
    -result {1 {} 2}
    -cleanup {rename sys {}}
}

test bdd-18.1 {appquant - wrong # args} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys exists_&
    }
    -cleanup {
	rename sys {}
    }
    -result {wrong # args:*}
    -returnCodes error
    -match glob
}

test bdd-18.2 {appquant - bad lhs} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys exists_& x {} garbage 1} result] $result $::errorCode
    }
    -cleanup {
	rename sys {}
    }
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-18.3 {appquant - bad rhs} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys exists_& x {} 1 garbage} result] $result $::errorCode
    }
    -cleanup {
	rename sys {}
    }
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-18.4 {appquant - bad var list} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys exists_& x \{ 1 1} result] $result $::errorCode
    }
    -cleanup {
	rename sys {}
    }
    -result \
	{1 {unmatched open brace in list} {TCL VALUE LIST BRACE}}
}

test bdd-18.5 {appquant - bad var} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys exists_& x garbage 1 1} result] $result $::errorCode
    }
    -cleanup {
	rename sys {}
    }
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-18.6 {appquant - bad var} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys exists_& x 0 1 1} result] $result $::errorCode
    }
    -cleanup {
	rename sys {}
    }
    -result \
	{1 {0 is not a variable} {BDD NotVariable 0}}
}

test bdd-18.7 {appquant - do nothing to nothing} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys exists_& x {} 1 1
	list [sys beadindex x] [sys unset x] [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result \
	{1 {} 2}
}

foreach q {exists forall} {
    foreach op {!= & < <= == > >= ^ nand nor |} {
	set combination ${q}_${op}
	test bdd-18.8-$combination {relational product} {*}{
	    -setup {
		bdd::system create sys
		sys nthvar a 0; sys notnthvar !a 0
		sys nthvar b 1; sys notnthvar !b 1
		sys nthvar c 2; sys notnthvar !c 2
		sys nthvar d 3; sys notnthvar !d 3
		sys nthvar e 4; sys notnthvar !e 4
		sys nthvar f 5; sys notnthvar !f 5
		sys nthvar g 6; sys notnthvar !g 6
		sys nthvar h 7; sys notnthvar !h 7
		
		sys &3 p a !b !c
		sys & p p !d
		sys &3 expr1 p !e !f
		sys &3 expr2 p !g !h
		
		sys &3 p !a b !c
		sys & p p !d
		sys &3 term1 p !e f
		sys &3 term2 p !g h
		sys | expr1 expr1 term1
		sys | expr2 expr2 term2
		
		sys &3 p !a !b c
		sys & p p !d
		sys &3 term1 p e !f
		sys &3 term2 p g !h
		sys | expr1 expr1 term1
		sys | expr2 expr2 term2
		
		sys &3 p !a !b !c
		sys & p p d
		sys &3 term1 p e f
		sys &3 term2 p g h
		sys | expr1 expr1 term1
		sys | expr2 expr2 term2
		
		sys unset p term1 term2
		
		sys $op opresult expr1 expr2
		sys $q result_sb {a b c d} opresult
		sys unset opresult
	    }
	    
	    -body {
		sys $combination result_is {a b c d} expr1 expr2
		list [expr {[sys beadindex result_is] \
				== [sys beadindex result_sb]}] \
		    [sys unset result_is result_sb expr1 expr2 \
			 a b c d e f g h !a !b !c !d !e !f !g !h] \
		    [sys gc]
	    }
	    -cleanup {rename sys {}}
	    -result {1 {} 2}
	}
    }
}

test bdd-19.1 {profile - wrong # args} {*}{
    -setup {bdd::system create sys}
    -body {sys profile}
    -cleanup {rename sys {}}
    -returnCodes error
    -match glob
    -result {wrong # args: *}
}

test bdd-19.2 {profile - bad expr} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [catch {sys profile garbage} result] $result $::errorCode
    }
    -cleanup {
	rename sys {}
    }
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-19.3 {profile constants} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	list [sys profile 0] [sys profile 1]
    }
    -cleanup {
	rename sys {}
    }
    -result {{} {}}
}

test bdd-19.4 {profile single variables} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
    }
    -body {
	list [sys profile 0] [sys profile 1] [sys profile a] [sys profile !a] \
	    [sys unset a !a] [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {0 0 1 1 {} 2}
}

test bdd-19.4 {profile single variables} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 1; sys notnthvar !b 1
    }
    -body {
	list [sys profile 0] [sys profile 1] \
	    [sys profile a] [sys profile !a] \
	    [sys profile b] [sys profile !b] \
	    [sys unset a !a b !b] [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {{0 0} {0 0} {1 0} {1 0} {0 1} {0 1} {} 2}
}

test bdd-19.5 {profile ==} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0
	sys nthvar b 1
	sys nthvar c 2
	sys nthvar d 3
	sys nthvar e 4
	sys nthvar f 5
    }
    -body {
	sys == eq1 a b
	sys == eq2 c d
	sys == eq3 e f
	sys &3 smart eq1 eq2 eq3
	sys == eq1 a d
	sys == eq2 b e
	sys == eq3 c f
	sys &3 dumb eq1 eq2 eq3
	list [sys profile smart] [sys profile dumb] \
	    [sys unset a b c d e f eq1 eq2 eq3 smart dumb] [sys gc]
    }
    -cleanup {
	rename sys {}
    }
    -result {{1 2 1 2 1 2} {1 2 4 8 4 2} {} 2}
}

test bdd-19.6 {profile 4-bit adder} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a1 0; sys nthvar b1 1; sys nthvar z1 2
	sys nthvar a2 3; sys nthvar b2 4; sys nthvar z2 5
	sys nthvar a4 6; sys nthvar b4 7; sys nthvar z4 8
	sys nthvar a8 9; sys nthvar b8 10; sys nthvar z8 11
    }
    -body {
	sys ^ sum1 a1 b1
	sys & carry2 a1 b1
	sys ^3 sum2 a2 b2 carry2
	sys median carry4 a2 b2 carry2
	sys ^3 sum4 a4 b4 carry4
	sys median carry8 a4 b4 carry4
	sys ^3 sum8 a8 b8 carry8
	sys median carry16 a8 b8 carry8
	sys == result sum1 z1
	sys == term sum2 z2
	sys & result result term
	sys == term sum4 z4
	sys & result result term
	sys == term sum8 z8
	sys & result result term
	sys == term carry16 0
	sys & result result term
	sys unset term sum1 sum2 sum4 sum8 carry2 carry4 carry8 carry16
	list [sys profile result] \
	    [sys unset result a1 a2 a4 a8 b1 b2 b4 b8 z1 z2 z4 z8] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {{1 2 3 2 3 4 2 3 4 2 2 2} {} 2}
}

test bdd-20.1 {simplify - wrong # args} {*}{
    -setup {
	bdd::system create sys
    }
    -body {
	sys simplify
    }
    -cleanup {
	sys destroy
    }
    -result {wrong # args: *}
    -match glob
    -returnCodes error
}

test bdd-20.2 {simplify - bad first expr} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 0; sys notnthvar !b 0
    }
    -body {list [catch {sys simplify c garbage b} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-20.3 {simplify - bad second expr} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 0; sys notnthvar !b 0
    }
    -body {list [catch {sys simplify c a garbage} result] $result $::errorCode}
    -cleanup {rename sys {}}
    -result \
	{1 {expression named "garbage" not found} {BDD ExprNotFound garbage}}
}

test bdd-20.4 {simplify - trivial cases} {*}{
    -setup {
	bdd::system create sys
	sys nthvar a 0; sys notnthvar !a 0
	sys nthvar b 0; sys notnthvar !b 0
    }
    -body {
	set trouble {}
	foreach {f d r} {
	    0 0 0
	    0 b 0
	    0 1 0
	    1 0 1
	    1 b 1
	    1 1 1
	    a 0 0
	    a a 1
	    a 1 a
	} {
	    sys simplify z $f $d
	    if {[sys beadindex z] != [sys beadindex $r]} {
		lappend trouble "simplify $f wrt $d did not yield $r"
	    }
	}
	sys unset a b !a !b z
	lappend trouble [sys gc]
    }
    -cleanup {
	sys destroy
    }
    -result 2
}

test bdd-20.5 {simplify - example 1} {*}{
    -setup {
	bdd::system create sys
	sys nthvar v1 0
	sys nthvar v2 1
	sys nthvar v3 2; sys notnthvar !v3 2
	sys nthvar v4 3
    }
    -body {
	sys & shouldbe v1 v2
	sys & temp !v3 v4 
	sys ?: f v1 v2 temp
	sys unset temp
	sys | domain v1 v3
	sys simplify is f domain
	list [expr {[sys beadindex is] == [sys beadindex shouldbe]}] \
	    [sys unset v1 v2 v3 !v3 v4 f domain is shouldbe] \
	    [sys gc]
    }
    -cleanup {
	sys destroy
    }
    -result {1 {} 2}
}

test bdd-20.6 {simplify - no change to system} {*}{
    -setup {
	bdd::system create sys
	sys nthvar v1 0; sys notnthvar !v1 0
	sys nthvar v2 1; sys notnthvar !v2 1
	sys nthvar v3 2; sys notnthvar !v3 2
    }
    -body {
	sys ?: f v1 !v3 !v2
	sys | domain v2 !v3
	sys simplify is f domain
	list [expr {[sys beadindex is] == [sys beadindex f]}] \
	    [sys unset v1 !v1 v2 !v2 v3 !v3 f domain is] \
	    [sys gc]
    }
    -cleanup {
	sys destroy
    }
    -result {1 {} 2}
}

test bdd-20.7 {simplify changes variables} {*}{
    -setup {
	bdd::system create sys
	sys nthvar v1 0; sys notnthvar !v1 0
	sys nthvar v2 1; sys notnthvar !v2 1
	sys nthvar v3 2; sys notnthvar !v3 2
    }
    -body {
	sys ?: f v1 v3 !v2
	sys | domain !v1 v3
	sys | sb v1 !v2
	sys simplify is f domain
	list [expr {[sys beadindex is] == [sys beadindex sb]}] \
	    [sys unset v1 !v1 v2 !v2 v3 !v3 f domain is sb] \
	    [sys gc]
    }
    -cleanup {
	sys destroy
    }
    -result {1 {} 2}
}

test bdd-34.1 {foreach_fullsat} {*}{
    -body {
	set result {}
	bdd::foreach_fullsat s {6 3 0 7 4 1} {1 0 3 1 6 0 7 0} {
	    set val 0
	    foreach bit $s {
		set val [expr {($val << 1) | $bit}]
	    }
	    lappend result [expr {$val & 0x7}] [expr {$val >> 3}]
	}
	set result
    }
    -result {0 2 2 2 0 3 2 3}
}

test bdd-34.2 {foreach_fullsat - return} {*}{
    -setup {
	proc x {} {
	    bdd::foreach_fullsat s {6 3 0 7 4 1} {1 0 3 1 6 0 7 0} {
		set val 0
		foreach bit $s {
		    set val [expr {($val << 1) | $bit}]
		}
		return [list [expr {$val & 0x7}] [expr {$val >> 3}]]
	    }
	}
    }
    -body {
	x
    }
    -cleanup {
	rename x {}
    }
    -result {0 2}
}

test bdd-34.3 {foreach_fullsat - break} {*}{
    -body {
	set result {}
	bdd::foreach_fullsat s {6 3 0 7 4 1} {1 0 3 1 6 0 7 0} {
	    set val 0
	    foreach bit $s {
		set val [expr {($val << 1) | $bit}]
	    }
	    if {$s eq {0 1 1 0 0 0}} {
		break
	    }
	    lappend result [expr {$val & 0x7}] [expr {$val >> 3}]
	}
	set result
    }
    -result {0 2 2 2}
}

test bdd-34.4 {foreach_fullsat - continue} {*}{
    -body {
	set result {}
	bdd::foreach_fullsat s {6 3 0 7 4 1} {1 0 3 1 6 0 7 0} {
	    set val 0
	    foreach bit $s {
		set val [expr {($val << 1) | $bit}]
	    }
	    if {$s eq {0 1 1 0 0 0}} {
		continue
	    }
	    lappend result [expr {$val & 0x7}] [expr {$val >> 3}]
	}
	set result
    }
    -result {0 2 2 2 2 3}
}

test bdd-34.5 {foreach_fullsat - code 6} {*}{
    -body {
	set result {}
	list [catch {
	    bdd::foreach_fullsat s {6 3 0 7 4 1} {1 0 3 1 6 0 7 0} {
		set val 0
		foreach bit $s {
		    set val [expr {($val << 1) | $bit}]
		}
		if {$s eq {0 1 1 0 0 0}} {
		    return -level 0 -code 6
		}
		lappend result [expr {$val & 0x7}] [expr {$val >> 3}]
	    }
	} v] $v $result
    }
    -result {3 {} {0 2 2 2}}
}
	    
test bdd-40.1 {eight queens} {*}{
    -setup {
	bdd::system create sys
	for {set i 0} {$i < 64} {incr i} {
	    sys nthvar v$i $i; sys notnthvar !v$i $i
	}
    }
    -body {
	sys := solution 1

	# iterate through the cells

	for {set row 0} {$row < 8} {incr row} {
	    sys := thisRowC 1
	    for {set col 7} {$col >= 0} {incr col -1} {
		sys := cellC 1
		set q1 v[expr {8* $row + $col}]

		# queen here <= no other queen in the same column

		sys := columnC 1
		for {set col2 7} {$col2 >= 0} {incr col2 -1} {
		    if {$col2 != $col} {
			set q2 v[expr {8 * $row + $col2}]
			sys & columnC columnC !$q2
		    }
		}
		sys <= columnC $q1 columnC
		sys & cellC cellC columnC
		sys unset columnC
		
		# queen here <= no other queen in the same row

		sys := rowC 1
		for {set row2 7} {$row2 >= 0} {incr row2 -1} {
		    if {$row2 != $row} {
			set q2 v[expr {8 * $row2 + $col}]
			sys & rowC rowC !$q2
		    }
		}
		sys <= rowC $q1 rowC
		sys & cellC cellC rowC
		sys unset rowC

		# queen here <= no other queen in the same diagonal

		sys := diag1C 1
		sys := diag2C 1
		for {set row2 7} {$row2 >= 0} {incr row2 -1} {
		    if {$row2 != $row} {
			set col2 [expr {$col + $row2 - $row}]
			if {$col2 >= 0 && $col2 < 8} {
			    set q2 v[expr {8 * $row2 + $col2}]
			    sys & diag1C diag1C !$q2
			}
			set col2 [expr {$col + $row - $row2}]
			if {$col2 >= 0 && $col2 < 8} {
			    set q2 v[expr {8 * $row2 + $col2}]
			    sys & diag2C diag2C !$q2
			}
		    }
		}
		sys <= diag1C $q1 diag1C
		sys & cellC cellC diag1C
		sys unset diag1C

		sys <= diag2C $q1 diag2C
		sys & cellC cellC diag2C
		sys unset diag2C

		# accumulate into the constraint set for the row

		sys & thisRowC thisRowC cellC
		sys unset cellC
	    }

	    # accumulate all constraints for a row into the solution

	    sys & solution solution thisRowC
	    sys unset thisRowC
	}
		
	# at least one queen in each column
	for {set col 0} {$col < 8} {incr col} {
	    sys := columnC 0
	    for {set row 0} {$row < 8} {incr row} {
		set q v[expr {8*$row + $col}]
		sys | columnC columnC $q
	    }
	    sys & solution solution columnC
	    sys unset columnC
	}

	# enumerate solutions
	sys foreach_sat A solution {
	    set sol [lrepeat 8 {}]
	    foreach {var value} $A {
		if {$value} {
		    lset sol [expr {$var/8}] [expr {$var%8}]
		}
	    }
	    lappend sols [join $sol {}]
	}

	# clean up
	sys unset solution
	for {set i 0} {$i < 64} {incr i} {
	    sys unset v$i !v$i
	}
	lappend sols [sys gc]

	lreverse $sols

    }
    -cleanup {rename sys {}}
    -result {2 04752613 05726314 06357142 06471352 13572064 14602753 14630752 15063724 15720364 16257403 16470352 17502463 20647135 24170635 24175360 24603175 24730615 25147063 25160374 25164073 25307461 25317460 25703641 25704613 25713064 26174035 26175304 27360514 30471625 30475261 31475026 31625704 31625740 31640752 31746025 31750246 35041726 35716024 35720641 36074152 36271405 36415027 36420571 37025164 37046152 37420615 40357162 40731625 40752613 41357206 41362750 41506372 41703625 42057136 42061753 42736051 46027531 46031752 46137025 46152037 46152073 46302751 47302516 47306152 50417263 51602473 51603742 52064713 52073164 52074136 52460317 52470316 52613704 52617403 52630714 53047162 53174602 53602417 53607142 57130642 60275314 61307425 61520374 62057413 62714053 63147025 63175024 64205713 71306425 71420635 72051463 73025164}
}

test bdd-40.2 {verification of a full adder} {
    -setup {
	bdd::system create sys
	sys nthvar x 0; sys notnthvar !x 0
	sys nthvar y 1; sys notnthvar !y 1
	sys nthvar ci 2; sys notnthvar !ci 2
    }
    -body {

	# specification is a truth table
	sys := spec_co 0
	sys := spec_z 0
	foreach {x y ci co z} {
	    0 0 0 0 0
	    0 0 1 0 1
	    0 1 0 0 1
	    0 1 1 1 0

	    1 0 0 0 1
	    1 0 1 1 0
	    1 1 0 1 0
	    1 1 1 1 1
	} {
	    if {$x} {
		set xlit x
	    } else {
		set xlit !x
	    }
	    if {$y} {
		set ylit y
	    } else {
		set ylit !y
	    }
	    if {$ci} {
		set cilit ci
	    } else {
		set cilit !ci
	    }
	    sys & input $cilit $ylit
	    sys & input input $xlit
	    if {$co} {
		sys | spec_co spec_co input
	    }
	    if {$z} {
		sys | spec_z spec_z input
	    }
	}

	# implementation is a gate array
	sys & gate1 x y
	sys ^ gate2 x y
	sys & gate3 gate2 ci
	sys ^ z gate2 ci
	sys | co gate1 gate3

	list [expr {[sys beadindex co] == [sys beadindex spec_co]}] \
	    [expr {[sys beadindex z] == [sys beadindex spec_z]}] \
	    [sys unset x y ci !x !y !ci spec_co spec_z input \
		 gate1 gate2 gate3 z co] \
	    [sys gc]
    }
    -cleanup {rename sys {}}
    -result {1 1 {} 2}
}

cleanupTests
return

# Local Variables:
# mode: tcl
# End:
